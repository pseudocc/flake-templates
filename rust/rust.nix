{ config, lib, inputs, ... }:

{
  options.rust = {
    toolchain = lib.mkOption {
      type = let
        toolchainModule = { ... }: {
          options.channel = lib.mkOption {
            type = lib.types.enum [ "stable" "beta" "nightly" ];
            default = "stable";
            description = "Rust toolchain channel.";
          };

          options.version = lib.mkOption {
            type = lib.types.str;
            default = "latest";
            description = "Rust toolchain version.";
          };

          options.profile = lib.mkOption {
            type = lib.types.enum [ "minimal" "default" ];
            default = "default";
            description = "Rust toolchain profile.";
          };
        };
      in lib.types.submodule toolchainModule;
      description = "Rust toolchain from rust-overlay.";
    };

    workspace = {
      check = lib.mkOption {
        type = lib.types.bool;
        default = true;
        description = "Whether to run tests in the workspace by default.";
      };

      cargo-nix = lib.mkOption {
        type = lib.types.path;
        description = "Cargo.nix generated by crate2nix.";
      };

      members = lib.mkOption {
        type = let
          memberModule = { ... }: {
            options.override = lib.mkOption {
              type = lib.types.attrs;
              default = {};
              description = "Overrides for the crate build.";
            };
          };
        in with lib.types; nullOr (attrsOf (submodule memberModule));
        default = null;
        description = "Crates in the workspace (for monorepos).";
      };
    };
  };

  config = let
    cfg = config.rust;
  in {
    perSystem = { system, ... }: let
      pkgs = import inputs.nixpkgs {
        inherit system;
        overlays = [ inputs.rust-overlay.overlays.default ];
      };

      rust-toolchain = with cfg.toolchain;
        if channel == "nightly" && version == "latest" then
          pkgs.rust-bin.selectLatestNightlyWith (toolchain: toolchain.${profile})
        else
          pkgs.rust-bin.${channel}.${version}.${profile};

      cargo-nix = import cfg.workspace.cargo-nix {
        inherit pkgs;
        buildRustCrateForPkgs = crate: pkgs.buildRustCrate.override {
          rustc = rust-toolchain;
          cargo = rust-toolchain;
        };
      };
      project = cargo-nix.rootCrate.packageId;
    in {
      devShells.default = pkgs.mkShell {
        inherit (rust-toolchain) name;
        packages = [
          pkgs.crate2nix
          rust-toolchain
        ];
      };

      checks = lib.mkIf cfg.workspace.check {
        ${project} = cargo-nix.rootCrate.build.override {
          runTests = true;
        };
      };

      packages =
        if cfg.workspace.members == null then rec {
          default = cargo-nix.rootCrate.build;
          ${project} = default;
        }
        else let
          mkMember = name: member:
            cargo-nix.workspaceMembers.${name}.build.override member.override;
        in lib.mapAttrs mkMember cfg.workspace.members;
    };
  };
}
